# Main bucket configuration
resource "aws_s3_bucket" "frontend" {
    bucket        = "${var.project_name}-frontend-bkt"
    force_destroy = true

    tags = {
        Name      = "${var.project_name}-frontend-bkt"
        ManagedBy = "terraform"
    }
}

# Block All Public Access
resource "aws_s3_bucket_public_access_block" "frontend" {
    bucket = aws_s3_bucket.frontend.id

    block_public_acls       = true
    block_public_policy     = true
    ignore_public_acls      = true
    restrict_public_buckets = true
}

# Server-Side Encryption
resource "aws_s3_bucket_server_side_encryption_configuration" "frontend" {
    bucket = aws_s3_bucket.frontend.id

    rule {
        apply_server_side_encryption_by_default {
            sse_algorithm     = "AES256"
        }
    }
}

# Versioning
resource "aws_s3_bucket_versioning" "frontend" {
    bucket = aws_s3_bucket.frontend.id

    versioning_configuration {
        status = "Enabled"
    }
}

# Lifecycle configuration
resource "aws_s3_bucket_lifecycle_configuration" "frontend" {
    bucket = aws_s3_bucket.frontend.id

    rule {
        id     = "delete-old-versions"
        status = "Enabled"

        filter {}

        noncurrent_version_expiration {
            noncurrent_days = 30
        }
    }
}

# Bucket policy allowing CloudFront OAC
resource "aws_s3_bucket_policy" "frontend" {
    bucket = aws_s3_bucket.frontend.id

    policy = jsonencode({
        Version = "2012-10-17"
        Statement = [
            {
                Sid    = "AllowCloudFrontRead"
                Effect = "Allow"

                Principal = {
                    Service = "cloudfront.amazonaws.com"
                }

                Action   = "s3:GetObject"
                Resource = "${aws_s3_bucket.frontend.arn}/*"

                Condition = {
                    StringEquals = {
                        "AWS:SourceArn" = var.cloudfront_distribution_arn
                    }
                }
            }
        ]
    })
}